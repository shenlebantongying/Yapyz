plugins {
    id 'java'
    id 'application'
    // For legacy jar, need better solution in future
    id "org.gradlex.extra-java-module-info" version "1.4.1"
}

group 'org.slbtty'
version '1.0'

repositories {
    mavenCentral()
}

ext {
    itextVersion = '8.0.0'
}

dependencies {
    implementation group: 'com.itextpdf', name: 'kernel', version: itextVersion

    implementation 'com.formdev:flatlaf:3.1.1'

    // Logging
    implementation 'org.tinylog:tinylog-api:2.6.2'
    implementation 'org.tinylog:tinylog-impl:2.6.2'

    // Hidden requirement of itext7
    runtimeOnly('org.tinylog:slf4j-tinylog:2.6.2') {
        transitive = true
    }
    implementation 'org.apache.lucene:lucene-core:9.7.0'
    implementation 'org.apache.lucene:lucene-queryparser:9.7.0'
}

java {
    modularity.inferModulePath = true

    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

compileJava {
    modularity.inferModulePath = true
}

extraJavaModuleInfo {
    automaticModule("commons-${itextVersion}.jar", "commons")
    automaticModule("io-${itextVersion}.jar", "io")
    automaticModule("kernel-${itextVersion}.jar", "kernal")
    // WHY ITEXT DEPENDS ON THIS??
    automaticModule("animal-sniffer-annotations-1.18.jar", "animal")
    automaticModule("bouncy-castle-connector-8.0.0.jar", "bouncyCastle")
}

application {
    mainModule = 'org.slbtty.yapyz'
    mainClass = 'org.slbtty.yapyz.YapyzLauncher'
}


dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.9.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.9.2')
}

test {
    useJUnitPlatform()
}

tasks.register('myJpackage') {
    group 'distribution'
    // TODO: what does installDist means?
    dependsOn 'installDist'
    doLast {
        def destDir = layout.buildDirectory.dir("myJpackage").get().toString()
        delete destDir
        def jpackageCMD = "jpackage " +
                "--type app-image " +
                "--input " + layout.buildDirectory.dir("install/Yapyz/lib").get().toString() + " " +
                "--dest  " + destDir + " " +
                "--name Yapyz " +
                "--main-jar Yapyz-1.0.jar " +
                "--main-class org.slbtty.yapyz.YapyzLauncher "
        def result = jpackageCMD.execute()

        println(jpackageCMD)
        println(result.getErr().text)
    }
}
